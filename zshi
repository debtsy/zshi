#!/usr/bin/env zsh

emulate -L zsh -o no_unset

if (( ARGC == 0 )); then
  print -ru2 -- 'Usage: zshi <init-file> [zsh-flag]...

The same as plain `zsh [zsh-flag]...` except that <init-file> gets
sourced after all startup files.'
  return 1
fi

() {
  local init=${1:a}
  shift
  local tmp
  {
    tmp=$(mktemp -d ${TMPDIR:-/tmp}/zsh.XXXXXXXXXX) || return
    local rc
    for rc in .zshenv .zprofile .zshrc .zlogin; do
      >$tmp/$rc <<<'{
  ZDOTDIR=$_zshi_zdotdir
  if [[ -f $ZDOTDIR/'$rc' && -r $ZDOTDIR/'$rc' ]]; then
    source -- "$ZDOTDIR"/'$rc'
  fi
} always {
  if [[ -o no_rcs ||
        -o login && '$rc' == .zlogin ||
        -o no_login && '$rc' == .zshrc ||
        -o no_login && -o no_interactive && '$rc' == .zshenv ]]; then
    unset _zshi_rcs _zshi_zdotdir
    command rm -rf -- '${(q)tmp}'
    source -- '${(q)init}'
  else
    _zshi_zdotdir=${ZDOTDIR:-~}
    ZDOTDIR='${(q)tmp}'
  fi
}' || return
    done
    _zshi_zdotdir=${ZDOTDIR:-~} ZDOTDIR=$tmp zsh "$@"
  } always {
    [[ -n $tmp ]] && rm -rf -- $tmp
  }
} "$@"
